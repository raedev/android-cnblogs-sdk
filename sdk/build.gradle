apply plugin: 'com.android.library'

Properties prop = new Properties()
prop.load(new FileInputStream(new File(rootProject.rootDir, "local.properties")))
def testCookie = prop.getProperty("test.cookie")
def clientId = prop.getProperty("sdk.clientId")
def clientSecret = prop.getProperty("sdk.clientSecret")

android {
    compileSdk rootProject.android.compileSdkVersion
    defaultConfig {
        minSdk rootProject.android.minSdkVersion
        targetSdk rootProject.android.targetSdkVersion
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // 测试用的登录Cookie，在官网上找到.Cnblogs.AspNetCore.Cookies的值
        buildConfigField 'String', 'TEST_COOKIE', "\"$testCookie\""
        // 开放接口的ClientID
        buildConfigField 'String', 'SDK_CLIENT_ID', "\"$clientId\""
        // 开放接口的ClientSECRET
        buildConfigField 'String', 'SDK_CLIENT_SECRET', "\"$clientSecret\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'com.cnblogs.sdk'

}

dependencies {
    // 单元测试
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // appcompat
    implementation "androidx.appcompat:appcompat:$VER_APP_COMPAT"

    // Room
    implementation "androidx.room:room-runtime:$VER_ROOM"
    annotationProcessor "androidx.room:room-compiler:$VER_ROOM"
    implementation "org.greenrobot:eventbus:$VER_EVENT_BUS"

    // rx
    implementation "io.reactivex.rxjava3:rxjava:$VER_RXJAVA"
    implementation "io.reactivex.rxjava3:rxandroid:$VER_RXANDROID"

    // retrofit
    implementation "com.squareup.retrofit2:retrofit:$VER_RETROFIT"
    implementation "com.squareup.retrofit2:converter-gson:$VER_RETROFIT"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$VER_RETROFIT"

    // Java Cookie
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$VER_OKHTTP"
    implementation "com.squareup.okhttp3:logging-interceptor:$VER_OKHTTP"

    implementation "org.jsoup:jsoup:$VER_JSOUP"
    implementation "com.google.code.gson:gson:$VER_GSON"
    implementation project(':other:swift')
}
